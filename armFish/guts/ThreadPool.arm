
ThreadPool_Create:
/*
	; in: rcx address of node affinity string
	       push   rbx
	       call   _SetThreadPoolInfo
		xor   ecx, ecx
	       call   Thread_Create
		mov   dword[threadPool.threadCnt], 1
		pop   rbx
		ret
*/
        stp  x21, x30, [sp, -16]!
         bl  Os_SetThreadPoolInfo
        mov  x1, 0
         bl  Thread_Create
        mov  w4, 1
        lea  x6, threadPool
        str  w4, [x6, ThreadPool.threadCnt]
        ldp  x21, x30, [sp], 16
        ret

ThreadPool_Destroy:
/*
	       push   rsi rdi rbx
		mov   edi, dword[threadPool.threadCnt]
*/
        stp  x21, x30, [sp, -16]!
        stp  x26, x27, [sp, -16]!
        lea  x6, threadPool
        ldr  w27, [x6, ThreadPool.threadCnt]

ThreadPool_Destroy.NextThread:
/*
		lea   ecx, [rdi-1]
	       call   Thread_Delete
		sub   edi, 1
		jnz   .NextThread
		mov   dword[threadPool.threadCnt], edi

		lea   rdi, [threadPool.nodeTable]
	       imul   ebx, dword[threadPool.nodeCnt], sizeof.NumaNode
		add   rbx, rdi
*/
        sub  w1, w27, 1
         bl  Thread_Delete
        sub  x27, x27, 1
       cbnz  x27, ThreadPool_Destroy.NextThread
        lea  x6, threadPool
        str  wzr, [x6, ThreadPool.threadCnt]

        lea  x27, threadPool + ThreadPool.nodeTable
        lea  x6, threadPool + ThreadPool.nodeCnt
        ldr  w4, [x6]
        mov  w5, sizeof.NumaNode
       madd  x21, x4, x5, x27
ThreadPool_Destroy.NextNumaNode:
/*
		mov   rcx, qword[rdi+NumaNode.cmhTable]
		mov   edx, sizeof.CounterMoveHistoryStats
	       call   _VirtualFree
		xor   eax, eax
		mov   qword[rdi+NumaNode.cmhTable], rax
		add   rdi, sizeof.NumaNode
		cmp   rdi, rbx
		 jb   .NextNumaNode
		pop   rbx rdi rsi
		ret
*/
        ldr  x1, [x27, NumaNode.cmhTable]
        mov  x2, sizeof.CounterMoveHistoryStats
         bl  Os_VirtualFree
        str  xzr, [x27, NumaNode.cmhTable]
        add  x27, x27, sizeof.NumaNode
        cmp  x27, x21
        blo  ThreadPool_Destroy.NextNumaNode
        ldp  x26, x27, [sp], 16
        ldp  x21, x30, [sp], 16
        ret

ThreadPool_ReadOptions:
/*
	       push   rbx rsi rdi
		mov   esi, dword[options.threads]
		mov   edi, dword[threadPool.threadCnt]
		cmp   edi, esi
		 je   .Skip
*/
        stp  x21, x30, [sp, -16]!
        stp  x26, x27, [sp, -16]!
        lea  x6, options
        ldr  w26, [x6, Options.threads]
        lea  x6, threadPool
        ldr  w27, [x6, ThreadPool.threadCnt]
        cmp  w27, w26
        beq  ThreadPool_ReadOptions.Skip

ThreadPool_ReadOptions.CheckCreate:
/*
		cmp   edi, esi
		 jb   .Create
*/
        cmp  w27, w26
        blo  ThreadPool_ReadOptions.Create

ThreadPool_ReadOptions.CheckDelete:
/*
		cmp   edi, esi
		 ja   .Delete
	       call   ThreadPool_DisplayThreadDistribution
*/
        cmp  w27, w26
        bhi  ThreadPool_ReadOptions.Delete
         bl  ThreadPool_DisplayThreadDistribution
ThreadPool_ReadOptions.Skip:
/*
		pop   rdi rsi rbx
		ret
*/
        ldp  x26, x27, [sp], 16
        ldp  x21, x30, [sp], 16
        ret
ThreadPool_ReadOptions.Create:
/*
		mov   ecx, edi
	       call   Thread_Create
		add   edi, 1
		mov   dword[threadPool.threadCnt], edi
		jmp   .CheckCreate
*/
        mov  w1, w27
         bl  Thread_Create
        add  w27, w27, 1
        lea  x6, threadPool
        str  w27, [x6, ThreadPool.threadCnt]
          b  ThreadPool_ReadOptions.CheckCreate
ThreadPool_ReadOptions.Delete:
/*
		sub   edi, 1
		mov   ecx, edi
	       call   Thread_Delete
		mov   dword[threadPool.threadCnt], edi
		jmp   .CheckDelete
*/
        sub  w27, w27, 1
        mov  w1, w27
         bl  Thread_Delete
        lea  x6, threadPool+ThreadPool.threadCnt
        str  w27, [x6]
          b  ThreadPool_ReadOptions.CheckDelete

ThreadPool_NodesSearched_TbHits:
/*
		xor   ecx, ecx
		xor   eax, eax
		xor   edx, edx
	.next_thread:
		mov   r8, qword[threadPool.threadTable+8*rcx]
		add   rax, qword[r8+Thread.nodes]
		add   rdx, qword[r8+Thread.tbHits]
		add   ecx, 1
		cmp   ecx, dword[threadPool.threadCnt]
		 jb   .next_thread
		ret
*/
        lea  x4, threadPool + ThreadPool.threadCnt
        lea  x5, threadPool + ThreadPool.threadTable
        ldr  w4, [x4]
        mov  w1, 0
        mov  x0, 0
        mov  x2, 0
ThreadPool_NodesSearched_TbHits.next_thread:
        ldr  x8, [x5, x1, lsl 3]
        ldr  x6, [x8, Thread.nodes]
        ldr  x7, [x8, Thread.tbHits]
        add  x0, x0, x6
        add  x2, x2, x7
        add  w1, w1, 1
        cmp  w1, w4
        blo  ThreadPool_NodesSearched_TbHits.next_thread
        ret

ThreadPool_DisplayThreadDistribution:
/*
	       push   rbx rsi rdi r14 r15
		lea   rdi, [Output]
		lea   rsi, [threadPool.nodeTable]
	       imul   r15d, dword[threadPool.nodeCnt], sizeof.NumaNode
		add   r15, rsi
*/
        stp  x21, x30, [sp, -16]!
        stp  x26, x27, [sp, -16]!
        stp  x24, x25, [sp, -16]!
        lea  x27, Output
        lea  x26, threadPool + ThreadPool.nodeTable
        lea  x6, threadPool + ThreadPool.nodeCnt
        ldr  w4, [x6]
        mov  x5, sizeof.NumaNode
       madd  x25, x4, x5, x26

ThreadPool_DisplayThreadDistribution.NextNode:
/*
		mov   rax, 'info str'
	      stosq
		mov   rax, 'ing node'
	      stosq
		mov   al, ' '
	      stosb
	     movsxd   rax, dword[rsi+NumaNode.nodeNumber]
	       call   PrintSignedInteger
		mov   rax, ' has thr'
	      stosq
		mov   rax, 'eads'
	      stosd
		 or   ebx, -1
*/
        lea  x1, sz_thread_format
        ldr  w0, [x26, NumaNode.nodeNumber]
        str  x0, [sp, -32]!
        add  x2, sp, 0
        mov  x3, 0
         bl  PrintFancy
        add  sp, sp, 32
        mov  w21, -1
ThreadPool_DisplayThreadDistribution.ThreadLoop:
/*
		add   ebx, 1
		cmp   ebx, dword[threadPool.threadCnt]
		jae   .ThreadLoopDone
		mov   rax, qword[threadPool.threadTable+8*rbx]
		cmp   rsi, qword[rax+Thread.numaNode]
		jne   .ThreadLoop
		mov   al, ' '
	      stosb
		mov   eax, ebx
	       call   PrintUnsignedInteger
		jmp   .ThreadLoop
*/
        add  w21, w21, 1
        lea  x6, threadPool
        ldr  w4, [x6, ThreadPool.threadCnt]
        cmp  w21, w4
        bhs  ThreadPool_DisplayThreadDistribution.ThreadLoopDone
        lea  x6, threadPool + ThreadPool.threadTable
        ldr  x0, [x6, x21, lsl 3]
        ldr  x4, [x0, Thread.numaNode]
        cmp  x26, x4
        bne  ThreadPool_DisplayThreadDistribution.ThreadLoop
        mov  w0, ' '
       strb  w0, [x27], 1
        mov  w0, w21
         bl  PrintUInt
          b  ThreadPool_DisplayThreadDistribution.ThreadLoop

ThreadPool_DisplayThreadDistribution.ThreadLoopDone:
/*
       PrintNewLine
		add   rsi, sizeof.NumaNode
		cmp   rsi, r15
		 jb   .NextNode
	       call   _WriteOut_Output
*/
        PrintNewLine
        add  x26, x26, sizeof.NumaNode
        cmp  x26, x25
        blo  ThreadPool_DisplayThreadDistribution.NextNode
         bl  Os_WriteOut_Output

ThreadPool_DisplayThreadDistribution.Return:
/*
		pop   r15 r14 rdi rsi rbx
		ret
*/
        ldp  x24, x25, [sp], 16
        ldp  x26, x27, [sp], 16
        ldp  x21, x30, [sp], 16
        ret


ThreadPool_StartThinking:
/*
	; in: rbp address of position
	;     rcx address of limits struct
	;            this will be copied to the global limits struct
	;            so that search threads can see it
	       push   rbp rbx rsi rdi r13 r14 r15
virtual at rsp
  .moveList rb sizeof.ExtMove*MAX_MOVES
  .lend rb 0
end virtual
.localsize = ((.lend-rsp+15) and (-16))

	 _chkstk_ms   rsp, .localsize
		sub   rsp, .localsize
		mov   rsi, rcx
		mov   r15, rbp
		mov   r14, qword[threadPool.threadTable+8*0]
	; rsi = address of limits
	; r15 = gui position
	; r14 = main thread
*/
ThreadPool_StartThinking.moveList = 0
ThreadPool_StartThinking.lend = sizeof.ExtMove*MAX_MOVES + ThreadPool_StartThinking.moveList
ThreadPool_StartThinking.localsize = (ThreadPool_StartThinking.lend + 15) & -16

        stp  x20, x30, [sp, -16]!
        stp  x21, x26, [sp, -16]!
        stp  x27, x23, [sp, -16]!
        stp  x24, x25, [sp, -16]!
        sub  sp, sp, ThreadPool_StartThinking.localsize

        mov  x26, x1
        mov  x25, x20
        lea  x6, threadPool
        ldr  x24, [x6, ThreadPool.threadTable + 8*0]
// x26 = address of limits
// x25 = gui position
// x24 = main thread

        mov  x1, x24
         bl  Thread_WaitForSearchFinished

        lea  x6, signals
       strb  wzr, [x6, Signals.stop]
       strb  wzr, [x6, Signals.stopOnPonderhit]
        lea  x1, limits
        mov  x2, x26
         bl  Limits_Copy

// first, get root moves from gui position

        lea  x26, limits+Limits.moveVec
        add  x27, sp, ThreadPool_StartThinking.moveList
        lea  x6, limits+Limits.moveVecSize
        ldr  w1, [x6]
       cbnz  w1, ThreadPool_StartThinking.use_searchmoves
        ldr  x21, [x20, Pos.state]
         bl  Gen_Legal
ThreadPool_StartThinking.have_moves:
        add  x26, sp, ThreadPool_StartThinking.moveList
        add  x1, x24, Thread.rootPos + Pos.rootMovesVec
         bl  RootMovesVec_Clear
ThreadPool_StartThinking.push_moves:
        cmp  x26, x27
        bhs  ThreadPool_StartThinking.push_moves_done
        add  x1, x24, Thread.rootPos + Pos.rootMovesVec
        ldr  w2, [x26, ExtMove.move]
        add  x26, x26, sizeof.ExtMove
         bl  RootMovesVec_PushBackMove
          b  ThreadPool_StartThinking.push_moves
ThreadPool_StartThinking.push_moves_done:

// next, copy to mainThread

        str  wzr, [x24, Thread.rootDepth]
        str  xzr, [x24, Thread.nodes]
        str  xzr, [x24, Thread.tbHits]
       strb  wzr, [x24, Thread.selDepth]
        str  wzr, [x24, Thread.resetCnt]
        mov  w4, MIN_RESETCNT   // check time asap
        str  w4, [x24, Thread.callsCnt]
        add  x1, x24, Thread.rootPos
         bl  Position_CopyToSearch

// switch x20 and x21 to position of mean thread

        add  x20, x24, Thread.rootPos
        ldr  x21, [x20, Pos.state]

// position is passed to threads by first converting to a fen
//   and then parsing this fen string
// the net effect is a fixed order on piece lists

         bl  Position_SetPieceLists

// copy position in main thread to workers

        str  xzr, [x24, Thread.nodes]   //filtering moves may have incremented mainThread.nodes
        mov  w27, 0
ThreadPool_StartThinking.next_thread:
        add  w27, w27, 1
        lea  x6, threadPool
        ldr  w4, [x6, ThreadPool.threadCnt]
        cmp  w27, w4
        bhs  ThreadPool_StartThinking.thread_copy_done

// get addres of worker thread
        lea  x6, threadPool + ThreadPool.threadTable
        ldr  x26, [x6, x27, lsl 3]

        add  x1, x26, Thread.rootPos
         bl  Position_CopyToSearch
        str  wzr, [x26, Thread.rootDepth]
        str  xzr, [x26, Thread.nodes]
        str  xzr, [x26, Thread.tbHits]
       strb  wzr, [x26, Thread.selDepth]
        str  wzr, [x26, Thread.resetCnt]
      mov32  w4, MAX_RESETCNT
        str  w4, [x26, Thread.callsCnt]

        ldr  x0, [x26, Thread.rootPos + Pos.rootMovesVec + RootMovesVec.table]
        ldr  x2, [x24, Thread.rootPos + Pos.rootMovesVec + RootMovesVec.table]
        
ThreadPool_StartThinking.copy_moves_loop:
        ldr  x4, [x24, Thread.rootPos + Pos.rootMovesVec + RootMovesVec.ender]
        cmp  x2, x4
        bhs  ThreadPool_StartThinking.copy_moves_done
        ldp  x4, x5, [x2]
        ldp  x6, x7, [x2, 16]
        stp  x4, x5, [x0]
        stp  x6, x7, [x0, 16]
        add  x0, x0, sizeof.RootMove
        add  x2, x2, sizeof.RootMove
          b  ThreadPool_StartThinking.copy_moves_loop

ThreadPool_StartThinking.copy_moves_done:
        str  x0, [x26, Thread.rootPos+Pos.rootMovesVec+RootMovesVec.ender]
          b  ThreadPool_StartThinking.next_thread
ThreadPool_StartThinking.thread_copy_done:

        mov  x1, x24
         bl  Thread_StartSearching

        add  sp, sp, ThreadPool_StartThinking.localsize
        ldp  x24, x25, [sp], 16
        ldp  x27, x23, [sp], 16
        ldp  x21, x26, [sp], 16
        ldp  x20, x30, [sp], 16
        ret

/*
if USE_SYZYGY
.check_tb:
	       call   Tablebase_RootProbe
		mov   byte[Tablebase_RootInTB], al
		xor   edx, edx
	       test   eax, eax
		jnz   .root_in
	       call   Tablebase_RootProbeWDL
		mov   byte[Tablebase_RootInTB], al
		xor   edx, edx
	       test   eax, eax
		 jz   .check_tb_ret
		cmp   edx, dword[Tablebase_Score]
	      cmovl   edx, dword[Tablebase_Cardinality]
    .root_in:
		mov   dword[Tablebase_Cardinality], edx
		lea   rcx, [rbp+Pos.rootMovesVec]
	       call   RootMovesVec_Size
		mov   qword[rbp-Thread.rootPos+Thread.tbHits], rax
	      movzx   edx, byte[Tablebase_UseRule50]
		mov   eax, dword[Tablebase_Score]
	       test   dl, dl
		jnz   .check_tb_ret
		mov   ecx, VALUE_MATE - MAX_PLY - 1
		cmp   eax, edx
	      cmovg   eax, ecx
		neg   ecx
		cmp   eax, edx
	      cmovl   eax, ecx
		mov   dword[Tablebase_Score], eax
		jmp   .check_tb_ret
end if

.use_searchmoves:
	; use the moves obtained from 'searchmoves' command
	; these have already been checked for legality
		xor   eax, eax
	      lodsw
	      stosq
		sub   ecx, 1
		jnz   .use_searchmoves
		jmp   .have_moves
*/
ThreadPool_StartThinking.use_searchmoves:

       ldrh  w0, [x26], 2
        str  x0, [x27], 8
        sub  w1, w1, 1
       cbnz  w1, ThreadPool_StartThinking.use_searchmoves
          b  ThreadPool_StartThinking.have_moves


